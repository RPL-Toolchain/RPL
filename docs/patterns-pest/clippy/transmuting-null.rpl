pattern transmuting-null

patt {
    #[diag = "transmute_null"]
    transmute_ptr_null_inline[$T: type] = fn _(..) {
        'null:
        let $null: *const $T;
        'cast:
        // This is a known null pointer, so transmuting it into a reference is undefined behavior.
        let $value: &$T = move $null as &$T (Transmute);
    } where {
        is_null($null)
    }

    #[diag = "transmute_null"]
    transmute_ptr_null[$T: type] = fn _(..) {
        'null:
        let $null: *const $T = std::ptr::null::<$T>();
        'cast:
        // This is a known null pointer, so transmuting it into a reference is undefined behavior.
        let $value: &$T = move $null as &$T (Transmute);
    }

    #[diag = "transmute_null"]
    #[deduplicate]
    transmute_const_zero_ptr[$T: type, $null: const(*const $T) /* where is_null_ptr(self) */] = fn _(..) {
        'cast:
        // This is a known null pointer, so transmuting it into a reference is undefined behavior.
        let $value: &$T = const $null as &$T (Transmute);
    } where {
        is_null_ptr($null)
    }
}

diag {
    transmute_null = {
        primary(cast) = "transmuting a known null pointer into a reference",
        note          = "`{$null}` is known to be a null pointer, so transmuting it into a reference is undefined behavior",
        name          = "transmuting_null",
    }
}
